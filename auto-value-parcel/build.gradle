import org.gradle.internal.jvm.Jvm

plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'java-library'
}

//<editor-fold desc="ShadowJar configuration">
// disable default jar tasks
configurations.runtimeOnly.artifacts.removeAll { it.archiveTask.is jar }
tasks.getByName('jar').enabled = false
// create extra configuration for shaded dependencies, so they're not included in the pom
def shadedConfig = configurations.create('compileShaded')
configurations.compileOnly.extendsFrom(shadedConfig)
shadowJar {
    minimize()
    classifier = ''
    configurations = [shadedConfig]
    relocate 'com.google.auto.common', 'autovalueparcel.shaded.com.google.auto.common'
    relocate 'com.google.common', 'autovalueparcel.shaded.com.google.common'
    relocate 'com.google.thirdparty', 'autovalueparcel.shaded.com.google.thirdparty'
    exclude 'afu/**'
    exclude 'org/**'
    exclude 'com/google/errorprone/annotations/**'
    exclude 'com/google/j2objc/annotations/**'
    exclude 'javax/**'
    exclude 'META-INF/maven/com.google.auto/auto-common/**'
}
artifacts {
    runtime shadowJar
    archives shadowJar
}
//</editor-fold>

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    annotationProcessor 'com.google.auto.service:auto-service:1.0-rc6'
    compileOnly 'com.google.auto.service:auto-service:1.0-rc6'

    implementation 'com.google.auto.value:auto-value:1.7'
    implementation 'com.google.auto.value:auto-value-annotations:1.7'
    compileShaded 'com.google.auto:auto-common:0.10'
    compileShaded 'com.google.guava:guava:28.1-jre'
    implementation 'com.squareup:javapoet:1.12.0'
    implementation project(':adapter')

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.truth:truth:1.0'
    testImplementation 'org.mockito:mockito-core:2.0.26-beta'
    testImplementation 'com.google.auto:auto-common:0.10'
    testImplementation 'com.google.testing.compile:compile-testing:0.18'
    testImplementation files(Jvm.current().getToolsJar())
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
